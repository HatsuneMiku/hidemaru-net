// HmChromeBrowserMode ver 1.2.6.2
// Copyright @ 2017 VSCode.life

disablebreak;

AnalyzeArgument:
    $url = getarg(0);
    // 引数があるなら、そこにきっと飛びたいのだろう。
    if ($url != "") {
        // ローカルにファイルが存在するなら、バイナリで開いて警告を回避しつつ、そのままHmChromeBrowserへと流していく。
        if (existfile($url)) {
            openfile "/h " + $url, binary;
        // ローカルに存在しないなら、URLと自動的にみなす。
        } else {
            newfile;
            disabledraw;
            $html = R"(<meta http-equiv="refresh" content="0; url=)" + $url + R"(">)";
            insert $html;
            clearupdated;
            title $url, 3;
            title -1, 3;
        }

    // 引数はなしで、「.mac」を対象にこのマクロを有効にするとダウンロードしようとするので「は？」となって鬱陶しすぎる。
    } else if (filetype == ".mac") {
		enablebreak;
        endmacro;
    }

HmChromeBrowser:
    #HMNET = loaddll( hidemarudir + @"\hm.NET.dll" );

    if (#HMNET) {

        #_ = dllfuncw(#HMNET, "SetDetachMethod", currentmacrodirectory + @"\HmChromeBrowserMode.dll", "HmChromeBrowserMode", "OnDetachMethod");

        // マクロを手動実行ならスイッチ。拡張子等の条件はなし。即ち、「実行されてないなら、実行」「実行されてるなら、閉じる」
        if (event == 0) {
            #ChromeBrowserIsNull = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmChromeBrowserMode.dll", "HmChromeBrowserMode", "IsNull" );
            if (#ChromeBrowserIsNull) {
                #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmChromeBrowserMode.dll", "HmChromeBrowserMode", "Create" );
            } else {
                #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmChromeBrowserMode.dll", "HmChromeBrowserMode", "Destroy" );
            }

        // ファイル読込時。拡張子の条件がマッチするなら。
        } else if (event == 1) {
            #_ = dllfuncw( #HMNET, "CallMethod", currentmacrodirectory + @"\HmChromeBrowserMode.dll", "HmChromeBrowserMode", "Create" );
        }

    } else {
        message("hm.NET.dllを読み込むことが出来ません");
    }


enablebreak;
endmacro;

